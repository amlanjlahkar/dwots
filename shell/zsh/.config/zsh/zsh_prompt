autoload -U colors && colors
autoload -Uz vcs_info

precmd() { vcs_info } # always load before rendering the prompt
setopt prompt_subst

# show marker for untracked files in a local git respository
function +vi-git-untracked() {
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
        git status --porcelain | grep -q '^?? ' 2> /dev/null ; then
        hook_com[staged]+='?'
    fi
}

# compare local git changes to remote changes
function +vi-git-st() {
    local ahead behind
    local -a gitstatus

    # Exit early in case the worktree is on a detached HEAD
    git rev-parse ${hook_com[branch]}@{upstream} >/dev/null 2>&1 || return 0

    local -a ahead_and_behind=(
        $(git rev-list --left-right --count HEAD...${hook_com[branch]}@{upstream} 2>/dev/null)
    )

    ahead=${ahead_and_behind[1]}
    behind=${ahead_and_behind[2]}

    (( $ahead )) && gitstatus+=( "↑${ahead}" )
    (( $behind )) && gitstatus+=( "↓${behind}" )

    hook_com[misc]+=${(j:/:)gitstatus}
}

# define styles for the vcs plugin
zstyle ':vcs_info:*+*:*' debug false
zstyle ':vcs_info:git*' check-for-changes true
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked git-st
zstyle ':vcs_info:git*' unstagedstr "!"
zstyle ':vcs_info:git*' formats "%F{7}•%f git:[%F{yellow}%b%f](%F{yellow}%u%f%%F{red}%c%f%F{magenta}%m%f) "

# prompt
CLINE=$'\n'
PROMPT='%F{7}%n%f in %F{blue}%3~%f ${vcs_info_msg_0_}%(!.#.%(0?.%F{green}>>%f.%F{red}>>%f)) '
